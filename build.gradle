ext.pluginClassNames='com.farmaprom.rundeck.plugin.MesosStepPlugin'
ext.rundeckPluginVersion= '1.1'

defaultTasks 'clean','build'
group 'com.farmaprom.rundeck'
apply plugin: 'java'

sourceCompatibility = 1.8
version = System.env.TRAVIS_TAG ?: "SNAPSHOT"

configurations{
    pluginLibs

    compile{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            [group: 'org.rundeck', name: 'rundeck-core', version: '2.10.1'],

    )

    pluginLibs(
            [group: 'org.apache.mesos', name: 'mesos', version: '1.4.1'],
            [group: 'com.google.protobuf', name: 'protobuf-java', version: '3.1.0'],
            [group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.5'],
            [group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'],
    )
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"

    manifest {
        def libList = configurations.pluginLibs.collect {'lib/' + it.name}.join(' ')

        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    jar.dependsOn(copyToLib)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
